buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url 'http://repo.spring.io/milestone' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath group: 'net.ltgt.gradle', name: 'gradle-apt-plugin', version: '0.6'
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.3.3.RELEASE'
        classpath group: 'org.springframework.build.gradle', name: 'propdeps-plugin', version: '0.0.7'
        classpath group: 'com.moowork.gradle', name: 'gradle-node-plugin', version: '0.12'
        classpath group: 'com.moowork.gradle', name: 'gradle-gulp-plugin', version: '0.12'
    }
}

plugins {
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.5"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'propdeps'
apply plugin: 'idea'
apply plugin: 'net.ltgt.apt'

ext {
    queryDslVersion = '3.7.2'
    lombokVersion = '1.16.8'
    dropwizardMetricsVersion = '3.1.2'
    assertj_core_version = '3.3.0'
    awaility_version = '1.7.0'
    commons_lang_version = '2.6'
    commons_io_version = '2.4'
    dropwizard_metrics_version = '3.1.2'
    logstash_logback_encoder_version = '4.6'
    javax_inject_version = '1'
    javax_transaction_version = '1.2'
    json_path_version = '0.9.1'
    jackson_version = '2.6.3'
    jjwt_version = '0.6.0'
    geronimo_javamail_1_4_mail_version = '1.8.4'
    hibernate_entitymanager_version = '4.3.11.Final'
    HikariCP_version = '2.4.3'
    liquibase_slf4j_version = '1.2.1'
    liquibase_core_version = '3.4.2'
    hibernate_validator_version = '5.2.1.Final'
    metrics_spark_reporter_version = '1.2'
    metrics_spring_version = '3.1.2'
    postgresql_version = '9.4-1203-jdbc42'
    spring_security_version = '4.0.4.RELEASE'
    springfox_version = '2.4.0'
}

group = 'io.koju.autopos'
version = '0.0.1-SNAPSHOT'

description = 'Auto Point of Sale'

repositories {
    maven { url 'http://repo.spring.io/milestone' }
    maven { url 'http://repo.spring.io/snapshot' }
    maven { url 'https://repository.jboss.org/nexus/content/repositories/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'http://repo.maven.apache.org/maven2' }
}

configurations {
    providedRuntime
}

dependencies {

    provided "org.projectlombok:lombok:$lombokVersion"
    apt "org.projectlombok:lombok:$lombokVersion"

    compile group: 'io.dropwizard.metrics', name: 'metrics-core'
    compile group: 'io.dropwizard.metrics', name: 'metrics-annotation', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-ehcache', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-graphite', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-healthchecks', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-jvm', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-servlet', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-json', version: dropwizard_metrics_version
    compile(group: 'io.dropwizard.metrics', name: 'metrics-servlets', version: dropwizard_metrics_version) {
        exclude(module: 'metrics-healthchecks')
    }
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-json-org', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hppc', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate4', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jackson_version
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jackson_version
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jackson_version
    compile(group: 'com.ryantenney.metrics', name: 'metrics-spring', version: metrics_spring_version) {
        exclude(module: 'metrics-core')
        exclude(module: 'metrics-healthchecks')
    }
    compile(group: 'com.zaxxer', name: 'HikariCP') {
        exclude(module: 'tools')
    }
    compile group: 'commons-lang', name: 'commons-lang', version: commons_lang_version
    compile group: 'commons-io', name: 'commons-io', version: commons_io_version
    compile group: 'javax.inject', name: 'javax.inject', version: javax_inject_version
    compile group: 'javax.transaction', name: 'javax.transaction-api'
    compile group: 'org.apache.geronimo.javamail', name: 'geronimo-javamail_1.4_mail', version: geronimo_javamail_1_4_mail_version
    compile group: 'org.hibernate', name: 'hibernate-core'

    compile(group: 'org.hibernate', name: 'hibernate-ehcache') {
        exclude(module: 'ehcache-core')
    }
    compile group: 'org.hibernate', name: 'hibernate-envers'
    compile group: 'org.hibernate', name: 'hibernate-validator'
    compile(group: 'org.liquibase', name: 'liquibase-core') {
        exclude(module: 'jetty-servlet')
    }
    compile group: 'com.mattbertolini', name: 'liquibase-slf4j', version: liquibase_slf4j_version
    compile group: 'org.springframework.boot', name: 'spring-boot-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    compile group: 'org.springframework.boot', name: 'spring-boot-loader-tools'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web')
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-cloudfoundry-connector'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-spring-service-connector'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-localconfig-connector'
    compile(group: 'org.springframework', name: 'spring-context-support') {
        exclude(module: 'quartz')
    }
    compile group: 'org.springframework.security', name: 'spring-security-config', version: spring_security_version
    compile group: 'org.springframework.security', name: 'spring-security-data', version: spring_security_version
    compile group: 'org.springframework.security', name: 'spring-security-web', version: spring_security_version
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: jjwt_version

    compile(group: 'io.springfox', name: 'springfox-swagger2', version: springfox_version)

    compile "com.mysema.querydsl:querydsl-jpa:$queryDslVersion"

    compile group: 'org.postgresql', name: 'postgresql'
    compile group: 'fr.ippon.spark.metrics', name: 'metrics-spark-reporter', version: metrics_spark_reporter_version
    testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: awaility_version
    testCompile group: 'com.jayway.jsonpath', name: 'json-path'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertj_core_version
    testCompile group: 'junit', name: 'junit'
    testCompile group: 'org.mockito', name: 'mockito-core'
    testCompile group: 'com.mattbertolini', name: 'liquibase-slf4j', version: liquibase_slf4j_version
    testCompile group: 'org.hamcrest', name: 'hamcrest-library'


    testCompile 'org.spockframework:spock-core'

    testCompile 'org.spockframework:spock-spring'

    optional group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
}


defaultTasks 'bootRun'

bootRepackage {
    mainClass = 'io.koju.autopos.Application'
}

war {
    rootSpec.exclude("**/tomcat-*.jar")
}

springBoot {
    mainClass = 'io.koju.autopos.Application'
    executable = true
}

bootRun {
    addResources = false
}

querydsl {
    library = "com.mysema.querydsl:querydsl-apt:$queryDslVersion"
    querydslSourcesDir = "src/querydsl/java"
    jpa = true
}

idea {
    module {
        generatedSourceDirs += file('src/querydsl/java')
        excludeDirs += file('node_modules')
//        downloadJavadoc = true
//        downloadSources = true
        jdkName = '1.8'
    }
}

test {
    ignoreFailures true
    reports.html.enabled = false
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn test
}

test.finalizedBy(testReport)

apply from: 'gradle/yeoman.gradle'

apply from: 'gradle/liquibase.gradle'

if (project.hasProperty('prod')) {
    apply from: 'gradle/profile_prod.gradle'
} else {
    apply from: 'gradle/profile_dev.gradle'
}

compileJava.dependsOn(processResources)

clean {
    delete "target"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

task stage(dependsOn: 'bootRepackage') {
}
